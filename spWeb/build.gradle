plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.0'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'com.bmuschko.docker-spring-boot-application' version '9.4.0'
}

group = 'xyz.defe.sp'
version = '0.0.1-SNAPSHOT'

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
}

ext {
	set('springCloudVersion', "2023.0.2")
}

dependencies {
	implementation fileTree(include: ["spCommon-0.0.1-SNAPSHOT.jar"], dir: "../spCommon/build/libs")

	implementation 'redis.clients:jedis:5.1.0'
	implementation 'com.google.code.gson:gson:+'
	implementation 'com.google.guava:guava:32.1.3-jre'
	implementation 'de.codecentric:spring-boot-admin-starter-client:3.1.8'

	implementation 'org.springframework.data:spring-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
	useJUnitPlatform()
}

//test.enabled = false

docker {
	def registryUrl = 'localhost:5000'
	springBootApplication {
		baseImage = 'eclipse-temurin:22-jre-alpine'
		ports = [8000]
		images = ["${registryUrl}/sp-web:0.0.1"]

		// Podman rootless service,comment out this line when use Docker
		url = 'unix:///run/user/1000/podman/podman.sock'
	}
}
